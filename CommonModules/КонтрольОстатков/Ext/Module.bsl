
Функция ОстаткиВалютыДостаточны(ПлатежнаяСистема, Валюта, Дата, ПутьКПолю, СуммаДокумента) Экспорт
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоступныеСредстваОстатки.ПлатежнаяСистема КАК ПлатежнаяСистема,
	|	ДоступныеСредстваОстатки.Валюта КАК Валюта,
	|	ДоступныеСредстваОстатки.СуммаВВалютеОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.ДоступныеСредства.Остатки(
	|			&Дата,
	|			ПлатежнаяСистема = &ПлатежнаяСистема
	|				И Валюта = &Валюта) КАК ДоступныеСредстваОстатки
	|ГДЕ
	|	ДоступныеСредстваОстатки.СуммаВВалютеОстаток < 0");
	Запрос.УстановитьПараметр("Валюта",Валюта);
	Запрос.УстановитьПараметр("ПлатежнаяСистема",ПлатежнаяСистема);
	Запрос.УстановитьПараметр("Дата", Новый Граница(Дата, ВидГраницы.Включая));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		// суммма выборки отрицательная
		Остаток = СуммаДокумента + Выборка.Сумма;
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Недостаточно средств для списания. Всего на счете %1: %2 %3'"),
			Строка(Выборка.ПлатежнаяСистема), Строка(Остаток), Строка(Выборка.Валюта));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ПутьКПолю, , Отказ);
		
	КонецЦикла;
	
	Возврат Не Отказ;
	
КонецФункции
