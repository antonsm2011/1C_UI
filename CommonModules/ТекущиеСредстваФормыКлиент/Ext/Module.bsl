
#Область ПрограммныйИнтерфейс

Процедура ОповеститьИзменениеСредств() Экспорт
	
	Оповестить("ИзменениеСредств");
	
КонецПроцедуры

Процедура ОбновитьДанныеТекущейСтроки(ЭлементСписок, ЗаполняемыеРеквизиты) Экспорт
	
	ТекСтрока = ЭлементСписок.ТекущиеДанные;

	Если Не ЗаполняемыеРеквизиты.Свойство("Ссылка") Тогда
		ЗаполняемыеРеквизиты.Вставить("Ссылка", ТекСтрока.Ссылка);
	КонецЕсли;
	
	Если Не ЗаполняемыеРеквизиты.Свойство("Дата") Тогда
		ЗаполняемыеРеквизиты.Вставить("Дата", ТекСтрока.Дата);
	КонецЕсли;
	
	РезультатОбновления = ТекущиеСредстваВызовСервера.ОбновитьВводСредств(ЗаполняемыеРеквизиты);
	
	ЗаполнитьЗначенияСвойств(ТекСтрока, РезультатОбновления);
	
	ОповеститьИзменениеСредств();
	
КонецПроцедуры

#Область ОбработчикиКомандСписка

Процедура УдалитьДокументСписка(Форма) Экспорт
	
	ИмяСписка = ТекущиеСредстваФормыКлиентСервер.ИмяТекущегоСписка(Форма);
	
	Если ИмяСписка <> Неопределено Тогда
		ТекСтрока = Форма.Элементы[ИмяСписка].ТекущиеДанные;
		Если ТекСтрока <> Неопределено Тогда
			ИдСтроки = ТекСтрока.ПолучитьИдентификатор();
			ТекущиеСредстваВызовСервера.УдалитьДокНепосредственно(ТекСтрока.Ссылка);
			СтрокаСписка = Форма[ИмяСписка].НайтиПоИдентификатору(ИдСтроки);
			Форма[ИмяСписка].Удалить(Форма[ИмяСписка].Индекс(СтрокаСписка));
			ОповеститьИзменениеСредств();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьДокументСписка(Форма, ДопРеквизиты) Экспорт
	
	ИмяСписка = ТекущиеСредстваФормыКлиентСервер.ИмяТекущегоСписка(Форма);
	
	Если ИмяСписка <> Неопределено Тогда
		НовыйДок = Форма[ИмяСписка].Добавить();
		
		ОповеститьИзменениеСредств();
	КонецЕсли;

КонецПроцедуры

Процедура ПеревестиВЧерновикДокументСписка(Форма) Экспорт
	
	ИмяСписка = ТекущиеСредстваФормыКлиентСервер.ИмяТекущегоСписка(Форма);
	
	Если ИмяСписка <> Неопределено Тогда
		ТекСтрока = Форма.Элементы[ИмяСписка].ТекущиеДанные;
		Если ТекСтрока <> Неопределено Тогда
			Результат = ТекущиеСредстваВызовСервера.ПометитьНаУдалениеДок(ТекСтрока.Ссылка);
			ЗаполнитьЗначенияСвойств(ТекСтрока, Результат);
		КонецЕсли;
		ОповеститьИзменениеСредств();
	КонецЕсли;

КонецПроцедуры

Процедура ОткрытьТекущийДокумент(Форма) Экспорт
	
	ИмяСписка = ТекущиеСредстваФормыКлиентСервер.ИмяТекущегоСписка(Форма);
	
	Если ИмяСписка <> Неопределено Тогда
		Если Форма.Элементы[ИмяСписка].Видимость Тогда
			ТекСтрока = Форма.Элементы[ИмяСписка].ТекущиеДанные;
			Если ТекСтрока <> Неопределено Тогда
				ОткрытьЗначение(ТекСтрока.Ссылка);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьРеквизитыПоУмолчанию(ЭлементСписок, ИсточникСредств = Неопределено, ПлатежнаяСистема = Неопределено) Экспорт
	
	ТекСтрока = ЭлементСписок.ТекущиеДанные;
	ТекСтрока.Дата = ТекущаяДата();
	Если ЗначениеЗаполнено(ИсточникСредств) тогда
		ТекСтрока.ИсточникСредств = ИсточникСредств;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПлатежнаяСистема) тогда
		ТекСтрока.ПлатежнаяСистема = ПлатежнаяСистема;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

