#Область ПрограммныйИнтерфейс

Процедура Создать (Этот) Экспорт
	
	Этот.Результат  = Новый ТабличныйДокумент;
	ЗадатьНачальныеНастройки(Этот);
	
КонецПроцедуры

Процедура СФормировать(Этот) Экспорт
	
	Для каждого Параметр Из Этот.Параметры Цикл
		
		Если Не ЗначениеЗаполнено(Параметр.Значение) Тогда
			КодОшибки = 1;
			Дашборды.СообщитьОбОшибке(КодОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьДинамикуКапитала(Этот);
	
КонецПроцедуры

Процедура РазместитьВИнтерфейсе(Форма, Этот) Экспорт
	
	Столбцы = "ДинамикаКапиталаСумма%,ДинамикаКапиталаИзм%";
	Родители = "ДинамикаКапиталаСумма,ДинамикаКапиталаИзм";
	
	Если Этот.Результат.Количество() > 0 Тогда
		Форма.Элементы.ДинамикаКапиталаРезультат.Видимость = Истина;
		Форма.Элементы.ДинамикаКапиталаПусто.Видимость = Ложь;
		ДашбордыФормы.ЗаполнитьЭлементыФормыПоТаблице(Форма, Этот.Результат, Столбцы, Родители, Неопределено);
	Иначе
		Форма.Элементы.ДинамикаКапиталаРезультат.Видимость = Ложь;
		Форма.Элементы.ДинамикаКапиталаПусто.Видимость = Истина;
	КонецЕсли;
	
	СтолбецКурсНеактулен = "ДККурсНеактуален%";
	РодительКурсНеактуален = "ДинамикаКапиталаКурсНеактуален";

	ДашбордыФормы.УстановитьПризнакНеактуальностиКурсаВалюты(Форма, Этот, СтолбецКурсНеактулен, РодительКурсНеактуален);

КонецПроцедуры

#КонецОбласти

Процедура СформироватьДинамикуКапитала(Этот)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ЗапросыВспомогательныеДанные.ТекстЗапросаСтоимостьКапитала();
	ВалютаРегл = ОбщегоНазначенияУИ.ПолучитьВалютуРегламентированногоУчета();
	Запрос.УстановитьПараметр("ВалютаРуб", ВалютаРегл);
	
	ДатаНачала = НачалоДня(Этот.Параметры.Дата);
	ПериодыОтчета = Новый Массив;
	ПериодыОтчета.Добавить(ДатаНачала);          // сегодня
	ПериодыОтчета.Добавить(ДатаНачала - 86400);  // вчера
	ПериодыОтчета.Добавить(ДатаНачала - 691200); // неделю назад
	ПериодыОтчета.Добавить(ДобавитьМесяц(ДатаНачала, -1));// месяц назад
	
	ДваМесяцаНазад = ДобавитьМесяц(ДатаНачала, -2);
	ПериодыОтчета.Добавить(ДваМесяцаНазад);// 2 месяца назад
	Запрос.УстановитьПараметр("НачалоПериода", ДваМесяцаНазад);
	Запрос.УстановитьПараметр("КонецПериода", Этот.Параметры.Дата);
	Запрос.УстановитьПараметр("Валюта", Справочники.Валюты.ПустаяСсылка());
	
	Результат = Запрос.Выполнить();
	
	//Если Результат.Пустой() Тогда
	//	Возврат;
	//КонецЕсли;
	
	ТабРезультат = Новый ТаблицаЗначений;
	ТабРезультат.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(9,0)));
	ТабРезультат.Колонки.Добавить("Изменение", Новый ОписаниеТипов("ФорматированнаяСтрока"));
	ТабРезультат.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТабРезультат.Колонки.Добавить("КурсНеактуален", Новый ОписаниеТипов("Булево"));
	
	ИмяОсновнойВалюты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВалютаРегл,"Символ");
	
	ДополнениеПериода = ЗапросыВспомогательныеДанные.ДополнениеПериода(
		ДваМесяцаНазад, Этот.Параметры.Дата, "День");
	
	ВыборкаВалют = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВалют.Следующий() Цикл
		
		Если ВыборкаВалют.СуммаВВалюте = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СохраненноеЗначение = 0;
		Сумма = 0;
		Выборка = ВыборкаВалют.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Для каждого ТекущийПериод Из ДополнениеПериода Цикл
			
			Если Выборка.НайтиСледующий(Новый Структура("Период", ТекущийПериод)) Тогда
				Если Выборка.ЕстьОстаток Тогда
					Сумма = Выборка.Сумма;
					СохраненноеЗначение = Выборка.СуммаВВалюте;
				Иначе
					Сумма = СохраненноеЗначение * Выборка.Курс;
				КонецЕсли;
			КонецЕсли;
					
			Если ПериодыОтчета.Найти(ТекущийПериод) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
	
			СтрокаТаб = ТабРезультат.Добавить();
			СтрокаТаб.Период = ТекущийПериод;
			СтрокаТаб.Сумма = Сумма;
			
		КонецЦикла;
		
	КонецЦикла;

	ТабРезультат.Свернуть("Период,КурсНеактуален,Изменение", "Сумма");
	
	ПредСумма = 0;
	Для каждого СтрокаТаб Из ТабРезультат Цикл
		
		Если ТабРезультат.Индекс(СтрокаТаб) > 0 Тогда
			
			Изменение = Окр(СтрокаТаб.Сумма - ПредСумма, 2);
			Если Изменение >= 0 Тогда
				ИзменениеФормат = Новый ФорматированнаяСтрока("+" + Строка(Изменение) + ИмяОсновнойВалюты,, ЦветаСтиля.ЦветПоступление);
			Иначе
				ИзменениеФормат = Новый ФорматированнаяСтрока(Строка(Изменение) + ИмяОсновнойВалюты,, ЦветаСтиля.ЦветСписание);
			КонецЕсли;
			
			СтрокаТаб.Изменение = ИзменениеФормат;
		КонецЕсли;
		
		ПредСумма = СтрокаТаб.Сумма;
		
	КонецЦикла;
	
	Если ТабРезультат.Количество() > 0 Тогда
		// Удаляем период 2 месяца назад, который использовался для получения динамика за 1 месяц назад.
		Если ТабРезультат.НайтиСтроки(Новый Структура("Период", ДваМесяцаНазад)).Количество() > 0 Тогда
			ТабРезультат.Удалить(ТабРезультат[0]);
		КонецЕсли;
		УдаляемыйЭлемент = ПериодыОтчета.Найти(ДваМесяцаНазад);
		ПериодыОтчета.Удалить(УдаляемыйЭлемент);
		
		// Добавляем периоды за которые курсы отсутствовали как неактуальные.
		Для каждого ТекущийПериод Из ПериодыОтчета Цикл
			Если ТабРезультат.Найти(ТекущийПериод, "Период") = Неопределено Тогда
				СтрокаТаб = ТабРезультат.Добавить();
				СтрокаТаб.Период = ТекущийПериод;
				СтрокаТаб.КурсНеактуален = Истина;
			КонецЕсли;
		КонецЦикла;
			
		// Выполняем сортировку по периоду и удаляем колонку, т.к. она не выводится в отчет.
		ТабРезультат.Сортировать("Период УБЫВ");
		ТабРезультат.Колонки.Удалить(2);
	КонецЕсли;
	
	Этот.Результат = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТабРезультат);
	
КонецПроцедуры

Процедура ЗадатьНачальныеНастройки(Этот)

		
	Этот.Параметры.Вставить("Дата");
	
	//Этот.Результат.ФиксацияСлева = 0;
	
КонецПроцедуры
