#Область ПрограммныйИнтерфейс

Процедура Создать (Этот) Экспорт
	
	Этот.ТипДиаграммы = ТипДиаграммы.График;
	Этот.Результат = Новый Диаграмма;
	
	ЗадатьНачальныеНастройки(Этот);
	
КонецПроцедуры

Процедура СФормировать(Этот) Экспорт
	
	Для каждого Параметр Из Этот.Параметры Цикл
		
		Если Не ЗначениеЗаполнено(Параметр.Значение) Тогда
			КодОшибки = 1;
			Дашборды.СообщитьОбОшибке(КодОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьКурсыТоваров(Этот);
	
КонецПроцедуры

Процедура РазместитьВИнтерфейсе(Форма, Этот) Экспорт
	
	Этот.ПриватныеСвойства.ОбработчикЛегенды.ИмяСобытия = "Нажатие";
	Этот.ПриватныеСвойства.ОбработчикЛегенды.Действие = "ОткрытьОтчетКурсовТоваров";
	Этот.ПриватныеСвойства.ГруппаЭлементаЛегенды = "ГруппаКурсыТоваровЛегенда";
	
	ДашбордыФормы.ЗаполнитьЛегенду(Форма, Этот);
	
	Форма.ГрафикКурсовТоваров = Этот.Результат;
	
КонецПроцедуры

#КонецОбласти

Процедура СформироватьКурсыТоваров(Этот)
	
	Этот.Результат.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",Этот.Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",Этот.Параметры.ОкончаниеПериода); 
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КурсыТоварныхРынков.Период КАК Период,
	|	КурсыТоварныхРынков.Валюта КАК Номенклатура,
	|	КурсыТоварныхРынков.Курс КАК КурсМакс,
	|	КурсыТоварныхРынков.Валюта.ЦветR КАК ЦветR,
	|	КурсыТоварныхРынков.Валюта.ЦветG КАК ЦветG,
	|	КурсыТоварныхРынков.Валюта.ЦветB КАК ЦветB,
	|	КурсыТоварныхРынков.Курс КАК КурсМин,
	|	КурсыТоварныхРынков.Номенклатура.Наименование КАК ИмяНоменклатуры
	|ИЗ
	|	РегистрСведений.КурсыТоварныхРынков КАК КурсыТоварныхРынков
	|ГДЕ
	|	КурсыТоварныхРынков.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|ИТОГИ
	|	МАКСИМУМ(КурсМакс),
	|	Максимум(ЦветR),
	|	Максимум(ЦветG),
	|	Максимум(ЦветB),
	|	МИНИМУМ(КурсМин),
	|	МАКСИМУМ(ИмяНоменклатуры)
	|ПО
	|	ОБЩИЕ,
	|	Номенклатура";
	
	ВыборкаОбщая = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаОбщая.Следующий() Цикл 
		
		Этот.Результат.МаксимальноеЗначение = ВыборкаОбщая.КурсМакс;
		Этот.Результат.МинимальноеЗначение = ВыборкаОбщая.КурсМин;
		//Этот.Результат.БазовоеЗначение = ВыборкаОбщая.КурсМин - 20;
		
		ВыборкаТоваров = ВыборкаОбщая.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаТоваров.Следующий() Цикл
			
			Серия = Этот.Результат.УстановитьСерию(ВыборкаТоваров.Номенклатура);
			Серия.Маркер = ТипМаркераДиаграммы.Нет;
			Серия.Текст = ВыборкаТоваров.ИмяНоменклатуры;
			
			Серия.Цвет = ОбщегоНазначенияУИ.ЦветRGB(
				ВыборкаТоваров.ЦветR, ВыборкаТоваров.ЦветG, ВыборкаТоваров.ЦветB);
				
			ВыборкаПериоды = ВыборкаТоваров.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПериоды.Следующий() Цикл
				
				Точка = Этот.Результат.УстановитьТочку(
					Формат(ВыборкаПериоды.Период, ДашбордыФормы.ФорматДаты(Этот.Параметры.Интервал)));
				Этот.Результат.УстановитьЗначение(Точка, Серия, ВыборкаПериоды.КурсМакс, ВыборкаПериоды.КурсМакс);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗадатьНачальныеНастройки(Этот)
	
	Этот.Результат.ТипДиаграммы = ТипДиаграммы.ГрафикСОбластями;
	Этот.Результат.ОтображатьЛегенду = Ложь;
	Этот.Результат.ОтображатьЗаголовок = Ложь;
	Этот.Результат.АвтомаксимальноеЗначение = Ложь;
	Этот.Результат.АвтоминимальноеЗначение = Ложь;
	Этот.Результат.РежимПолупрозрачности = РежимПолупрозрачностиДиаграммы.НеИспользовать;

	Этот.Результат.ОбластьПостроения.ОтображатьШкалу = Истина;
	Этот.Результат.ОбластьПостроения.ЛинииШкалы = Новый Линия(ТипЛинииДиаграммы.Сплошная);
	Этот.Результат.ОбластьПостроения.Лево = 0.01;
	Этот.Результат.ОбластьПостроения.ОтображатьЛинииЗначенийШкалы = Истина;
	Этот.Результат.ОбластьПостроения.ЛинииШкалы = Новый Линия(ТипЛинииДиаграммы.Пунктир);
	
	Этот.Результат.ОбластьПостроения.ОтображатьПодписиШкалыЗначений = Истина;

	Этот.Результат.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки); 
	
	Этот.Параметры.Вставить("НачалоПериода");
	Этот.Параметры.Вставить("ОкончаниеПериода");
	Этот.Параметры.Вставить("Интервал");
	
	ОбработчикЛегенды = Новый Структура("ИмяСобытия,Действие");
	Этот.ПриватныеСвойства.Вставить("ОбработчикЛегенды", ОбработчикЛегенды);
	
	Этот.ПриватныеСвойства.Вставить("ГруппаЭлементаЛегенды");
	
КонецПроцедуры
