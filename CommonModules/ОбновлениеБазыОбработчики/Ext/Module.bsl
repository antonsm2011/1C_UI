Процедура ПерейтиНаВерсию_1_0_0_00() Экспорт 
	
	Пользователи.СоздатьАдминистратора();
	
	Валюты = Новый Массив;
	Валюты.Добавить("643"); // рубль
	Валюты.Добавить("840"); // доллар
	РаботаСКурсамиВалют.ДобавитьВалютыПоКоду(Валюты);
	
	РегВалюта = Справочники.Валюты.НайтиПоКоду("643");
	ВалютаДоллар = Справочники.Валюты.НайтиПоКоду("840");
	
	Константы.ВалютаРегламентированногоУчета.Установить(РегВалюта);
	
	// Заполнение настроек пользователя по умолчанию
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПерсональныеНастройки", "ОсновнаяПлатежнаяСистема", Справочники.ПлатежныеСистемы.ПустаяСсылка(), , ИмяПользователя());
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПерсональныеНастройки", "ОсновнаяВалюта", ВалютаДоллар, , ИмяПользователя());
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПерсональныеНастройки", "ОсновнойИсточникСредств", Перечисления.ВидыИсточниковСредств.ЗарплатаВОффлайне, , ИмяПользователя());
	
	// Нужно акитивизировать группу с свойствами справочника Объекты_Инвестиций
	Попытка 
		УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_ОбъектыИнвестиций", 
		Новый Структура("Используется", Истина));
		
		ЗаписьЖурналаРегистрации("Переход на версию 1.0.0.00", 
			УровеньЖурналаРегистрации.Информация, 
			Метаданные.Справочники.ОбъектыИнвестиций,, 
			"Обновление элемента предопределенных данных выполнено успешно");
		
	Исключение
		ЗаписьЖурналаРегистрации("Переход на версию 1.0.0.00", 
			УровеньЖурналаРегистрации.Информация, 
			Метаданные.Справочники.ОбъектыИнвестиций,, 
			"Обновление элемента предопределенных данных (Справочники.ОбъектыИнвестиций.Справочник_ОбъектыИнвестиций завершилось неудачно");
	КонецПОпытки;
	
	// Создание предопределенного вида инвестиции с назначенным набором свойств
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВидыИнвестиций.Ссылка
	|ИЗ
	|	Справочник.ВидыИнвестиций КАК ВидыИнвестиций
	|ГДЕ
	|	ВидыИнвестиций.Ссылка = &Ссылка
	|	И ВидыИнвестиций.НаборСвойств = &ПустойНаборСвойств");
	Запрос.УстановитьПараметр("Ссылка", Справочники.ВидыИнвестиций.Квартира);
	Запрос.УстановитьПараметр("ПустойНаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Если Не Справочники.ВидыИнвестиций.ПолучитьИнициализациюПредопределенныхДанных() Тогда
			Справочники.ВидыИнвестиций.УстановитьИнициализациюПредопределенныхДанных(Истина);
			ЭлементКвартира = Справочники.ВидыИнвестиций.СоздатьЭлемент();
			ЭлементКвартира.ИмяПредопределенныхДанных = "Квартира";
		Иначе
			ЭлементКвартира = Справочники.ВидыИнвестиций.Квартира.ПолучитьОбъект();
		КонецЕсли;
		
		ЭлементКвартира.Наименование = НСтр("ru = 'Квартира'");
		УправлениеСвойствами.ПередЗаписьюВидаОбъекта(ЭлементКвартира, "Справочник_ОбъектыИнвестиций");
		
		// Добавляем стоимость для набора с квартирой
		НаборСвойств = ЭлементКвартира.НаборСвойств.ПолучитьОбъект();
		СтрокаСвойства = НаборСвойств.ДополнительныеРеквизиты.Добавить();
		СтрокаСвойства.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.Стоимость;
		НаборСвойств.Записать();
		
		ЭлементКвартира.Записать();
		
	КонецЕсли;

КонецПроцедуры

Процедура ПерейтиНаВерсию_1_0_0_01() Экспорт 
	
	
КонецПроцедуры

Процедура ЗаполнитьНастройки() Экспорт
	
	ПервыйЗапускВыполнен = Константы.ПервыйЗапускВыполнен.Получить();
	
	Если ПервыйЗапускВыполнен Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектРубль = Справочники.Валюты.Рубль.ПолучитьОбъект();
	ОбъектРубль.КодВалюты = "643";
	ОбъектРубль.Записать();
	
	ОбъектДоллар = Справочники.Валюты.Доллар.ПолучитьОбъект();
	ОбъектДоллар.КодВалюты = "840";
	ОбъектДоллар.Записать();
	
	МЗ = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
	МЗ.Период = НачалоГода(ТекущаяДата());
	МЗ.Валюта = ОбъектРубль.Ссылка;
	МЗ.Курс = 1;
	МЗ.Записать();
	
	Константы.ОсновнаяВалюта.Установить(ОбъектРубль.Ссылка);
	Константы.ДополнительнаяВалюта.Установить(ОбъектДоллар.Ссылка);
	Константы.ДатаНачалаЗагрузкиКурсов.Установить(НачалоМесяца(ТекущаяДата()));
	
	Константы.ПервыйЗапускВыполнен.Установить(Истина);
		
КонецПроцедуры