
Функция ПроверитьАктуальностьКурсов(Форма, КурсыОбновляютсяАвтоматически) Экспорт
	
	ПараметрыАктуальности = РаботаСКурсамиАльтернативныйВызовСервера.ПараметрыАктуальностиКурсовВалют();
	
	Если Не ПараметрыАктуальности.КурсАктуален Тогда
		Если КурсыОбновляютсяАвтоматически тогда
			ЗапуститьФоновоеОбновлениеКурсов(Форма);
		Иначе
			Форма.Элементы.ГруппаТребуетсяАктуализация.Видимость = Истина;
			
			Описание1 = Новый ФорматированнаяСтрока(НСтр("ru = 'Курсы валют и товаров актуальны на '"));
			
			ЖирныйШрифт = Новый Шрифт(,,Истина);
			
			Описание2 = Новый ФорматированнаяСтрока(Формат(ПараметрыАктуальности.ДатаАктуальности, "ДФ=dd.MM.yyyy"), ЖирныйШрифт);
			Описание3 = Новый ФорматированнаяСтрока(НСтр( "ru = '. Рекомендуется обновить курсы (требуется интернет)'"));

			Форма.Элементы.ТекстНеобходимостиАктуализации.Заголовок = Новый ФорматированнаяСтрока(Описание1, Описание2, Описание3);
		КонецЕсли;
	КонецЕсли;

КонецФункции

Процедура ЗапуститьФоновоеОбновлениеКурсов(Форма) Экспорт
	
	СкрытьЭлементыАктуализации(Форма);
	
	ПараметрыЗадания = Новый Структура;
	
	ДанныеФоновогоЗадания = АктуализацияФоновыеЗаданияВызовСервера.ВыполнитьВФоне(
		"АктуализацияКурсовСервер.ОбновитьКурсыВалютИТоваров", ПараметрыЗадания, Форма.УникальныйИдентификатор);
	
	Форма.ИдентификаторЗадания = ДанныеФоновогоЗадания.ИдентификаторЗадания;
	Форма.АдресРезультата = ДанныеФоновогоЗадания.АдресРезультата;
	Если АктуализацияФоновыеЗаданияВызовСервера.ПроверитьЗаданиеВыполнено(Форма.ИдентификаторЗадания) Тогда
		ЗавершитьАктуализациюКурсов(Форма);
	Иначе
		Форма.Элементы.ГруппаИдетАктуализация.Видимость = Истина;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(Форма.ПараметрыОбработчикаОжиданияАктуализации);
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальностьЗаданияКурсов", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьАктуальностьЗаданияОбновленияКурсов(Форма) Экспорт
	
	Если АктуализацияФоновыеЗаданияВызовСервера.ПроверитьЗаданиеВыполнено(Форма.ИдентификаторЗадания) Тогда
		ЗавершитьАктуализациюКурсов(Форма);
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(Форма.ПараметрыОбработчикаОжиданияАктуализации);
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальностьЗаданияКурсов",
			Форма.ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗавершитьАктуализациюКурсов(Форма)
	
	Форма.Элементы.ГруппаИдетАктуализация.Видимость = Ложь;
	
	КурсыОбновлены = ПолучитьИзВременногоХранилища(Форма.АдресРезультата);
	
	Если КурсыОбновлены Тогда
		Форма.ЗапуститьФоновоеОбновлениеОтчетов();
	Иначе
		Форма.Элементы.ГруппаОшибкаАктуализации.Видимость = Истина;
	КонецЕсли;
	
КОнецПРоцедуры

Процедура СкрытьЭлементыАктуализации(Форма) Экспорт
	
	Форма.Элементы.ГруппаОшибкаАктуализации.Видимость = Ложь;
	Форма.Элементы.ГруппаТребуетсяАктуализация.Видимость = Ложь;
	Форма.Элементы.ГруппаИдетАктуализация.Видимость = Ложь;
	
КонецПроцедуры


