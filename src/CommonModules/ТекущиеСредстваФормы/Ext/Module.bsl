
Процедура ЗаполнитьИнфоСредств(Форма) Экспорт
	
	ВалютаСредств1 = Константы.ОсновнаяВалюта.Получить();
	ВалютаСредств2 = Константы.ДополнительнаяВалюта.Получить();
	
	ЛимитПанелей = 3;
	
	Для Счетчик = 1 По ЛимитПанелей Цикл
		Форма.Элементы["ГруппаПлатежнаяСистема"+ Счетчик].Видимость = Ложь;
		Для Счетчик2 = 1 По ЛимитПанелей Цикл
			Форма.Элементы["СредстваВалюта" + Строка(Счетчик) + Строка(Счетчик2)].Видимость = Ложь;
			Форма.Элементы["СредстваСумма" + Строка(Счетчик) + Строка(Счетчик2)].Видимость = Ложь;
		КонецЦикла;
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоступныеСредстваОстатки.СуммаВВалютеОстаток КАК Остаток,
	|	ДоступныеСредстваОстатки.Валюта КАК Валюта,
	|	ДоступныеСредстваОстатки.ПлатежнаяСистема КАК ПлатежнаяСистема
	|ИЗ
	|	РегистрНакопления.ДоступныеСредства.Остатки(, ) КАК ДоступныеСредстваОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Валюта
	|ИТОГИ
	|	СУММА(Остаток)
	|ПО
	|	ПлатежнаяСистема");
	
	//Валюты = Новый Массив;
	//Валюты.Добавить(ВалютаСредств1);
	//Валюты.Добавить(ВалютаСредств2);
	//Запрос.УстановитьПараметр("Валюты", Валюты);
	
	ВыборкаПС = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Счетчик = 0;
	Пока ВыборкаПС.Следующий() Цикл
		
		Счетчик = Счетчик + 1;
		
		Форма.Элементы["ГруппаПлатежнаяСистема"+ Счетчик].Видимость = Истина;
		
		Форма.Элементы["ПлатежнаяСистема"+Счетчик].Заголовок = ВыборкаПС.ПлатежнаяСистема;
		
		ВыборкаДетали = ВыборкаПС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Счетчик2 = 0;
		Пока ВыборкаДетали.Следующий() Цикл
			
			Счетчик2 = Счетчик2 + 1;
			
			ЭлементВалюта = Форма.Элементы["СредстваВалюта" + Строка(Счетчик) + Строка(Счетчик2)];
			ЭлементВалюта.Заголовок = ВыборкаДетали.Валюта;
			ЭлементВалюта.Видимость = Истина;
			
			ЭлементСредства = Форма.Элементы["СредстваСумма" + Строка(Счетчик) + Строка(Счетчик2)];
			ЭлементСредства.Заголовок = Формат(ВыборкаДетали.Остаток, "ЧДЦ=; ЧРГ=' '");
			ЭлементСредства.Видимость = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСпискиДокументов(Форма) Экспорт
	
	ТекстыЗапросов = (
	"ВЫБРАТЬ
	|	ВводСредств.Дата КАК Дата,
	|	ВводСредств.Ссылка КАК Ссылка,
	|	ВводСредств.Валюта КАК Валюта,
	|	ВводСредств.Сумма КАК Сумма,
	|	ВводСредств.ПометкаУдаления КАК ПометкаУдаления,
	|	ВводСредств.ПлатежнаяСистема,
	|	ВводСредств.ИсточникСредств
	|ИЗ
	|	Документ.ВводСредств КАК ВводСредств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыводСредств.Валюта,
	|	ВыводСредств.Сумма,
	|	ВыводСредств.ОбъектИнвестиций,
	|	ВыводСредств.ИнвестированыВПроект,
	|	ВыводСредств.Ссылка,
	|	ВыводСредств.ПометкаУдаления,
	|	ВыводСредств.Дата,
	|	ВыводСредств.ПлатежнаяСистема
	|ИЗ
	|	Документ.ВыводСредств КАК ВыводСредств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереводМеждуЭПС.ВалютаИсточник,
	|	ПереводМеждуЭПС.СуммаИсточник,
	|	ПереводМеждуЭПС.Дата,
	|	ПереводМеждуЭПС.Ссылка,
	|	ПереводМеждуЭПС.ПлатежнаяСистемаПриемник,
	|	ПереводМеждуЭПС.СуммаПриемник,
	|	ПереводМеждуЭПС.ВалютаПриемник,
	|	ПереводМеждуЭПС.ПлатежнаяСистемаИсточник,
	|	ПереводМеждуЭПС.ПометкаУдаления
	|ИЗ
	|	Документ.ПереводМеждуЭПС КАК ПереводМеждуЭПС");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстыЗапросов;
	Результат = Запрос.ВыполнитьПакет();
	
	Форма.СписокДокументовДоход.Загрузить(Результат[0].Выгрузить());
	Для каждого СтрокаТаб Из Форма.СписокДокументовДоход Цикл
		СтрокаТаб.СостояниеДокумента = КартинкаСостоянияДокумента(СтрокаТаб.ПометкаУдаления);
	КонецЦикла;
	
	Форма.СписокДокументовРасход.Загрузить(Результат[1].Выгрузить());
	Для каждого СтрокаТаб Из Форма.СписокДокументовРасход Цикл
		СтрокаТаб.СостояниеДокумента = КартинкаСостоянияДокумента(СтрокаТаб.ПометкаУдаления);
	КонецЦикла;
	
	Форма.СписокДокументовПеревод.Загрузить(Результат[2].Выгрузить());
	Для каждого СтрокаТаб Из Форма.СписокДокументовПеревод Цикл
		СтрокаТаб.СостояниеДокумента = КартинкаСостоянияДокумента(СтрокаТаб.ПометкаУдаления);
	КонецЦикла;
	
КонецПРоцедуры

// Возникает при нажатии кнопки Del пользователем на списке
//
// Параметры:
//  ЭлементСписок		 - 	 - 
//  ЗаполняемыеРеквизиты - 	 - 
//  ЭтоНоваяСтрока		 - 	 - 
//  ОтменаРедактирования - 	 - 
//
Процедура ОбработатьУдалениеДокументаНаСервере(Форма) Экспорт
	
	ИмяСписка = ТекущиеСредстваФормыКлиентСервер.ИмяТекущегоСписка(Форма);
	
	УдаляемыеДокументы = Новый Массив;
	ПроводимыеДокументы = Новый Массив;
	
	Для каждого ВыделеннаяСтрока Из Форма.Элементы[ИмяСписка].ВыделенныеСтроки Цикл
		СтрокаТаб = Форма[ИмяСписка].НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если СтрокаТаб <> Неопределено Тогда
			Если СтрокаТаб.ПометкаУдаления Тогда
				ПроводимыеДокументы.Добавить(Новый Структура("Ссылка, Дата", СтрокаТаб.Ссылка, СтрокаТаб.Дата));
			Иначе
				УдаляемыеДокументы.Добавить(СтрокаТаб.Ссылка);
			КонецЕсли;
			
			СтрокаТаб.ПометкаУдаления = Не СтрокаТаб.ПометкаУдаления;
			СтрокаТаб.СостояниеДокумента = КартинкаСостоянияДокумента(СтрокаТаб.ПометкаУдаления);
			
		КонецЕсли;
	КонецЦикла;
	
	ТекущиеСредстваВызовСервера.ОбновитьДокуметыДвиженияДенежныхСредств(ПроводимыеДокументы);
	ТекущиеСредстваВызовСервера.УдалитьДокументыДенежныхСредств(УдаляемыеДокументы);
	
	ТекущиеСредстваФормы.ЗаполнитьИнфоСредств(Форма);
	
КонецПроцедуры

Функция КартинкаСостоянияДокумента(ПометкаУдаления) Экспорт
	Возврат  ?(ПометкаУдаления, БиблиотекаКартинок.Ожидается, БиблиотекаКартинок.Успешно);
КонецФункции
